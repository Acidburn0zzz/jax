#= require_self
#= require_tree "./material"

class Jax.Material
  constructor: (options, @name = "generic") ->
    @shader = new Jax.Shader.Program @name
    @layers = []
    @assigns = {}
    options = Jax.Util.normalizeOptions options, {}
    for key, value of options
      switch key
        when 'layers'  then @addLayer layer for layer in value
        else @[key] = value
    
  @define 'vertex', get: -> @shader.vertex
  @define 'fragment', get: -> @shader.fragment
  
  addLayer: (options) ->
    if typeof options is 'string' then options = { type: options }
    if options instanceof Jax.Material.Layer
      @layers.push options
      return options
    
    options = Jax.Util.normalizeOptions options, {}
    Klass = Jax.Material[options.type]
    throw new Error "#{@name}: Material layer type #{options.type} could not be found" unless Klass
    options.shader or= Klass.shadder || Jax.Util.underscore options.type
    layer = new Klass options, this
    if layer instanceof Jax.Material
      throw new Error """
        #{@name}: Custom material layers now inherit from Jax.Material.Layer instead of Jax.Material.
        Please also note that the constructor takes two arguments: the classic `options` hash, and a
        reference to the layer's parent instance of `Jax.Material`, which should be passed to `super`.
        Please see the documentation for more information.
      """
    @layers.push layer
    layer
    
  clearAssigns: ->
    assigns = @assigns
    for k of assigns
      assigns[k] = undefined
    true
  
  ###
  Renders a single mesh, taking as many passes as the material's layers indicate
  are needed, and then returns the number of passes it actually took.
  ###
  renderMesh: (context, mesh, model) ->
    numPassesRendered = 0
    numPassesRequested = 0
    mesh.data.context = context
    for layer in @layers
      layer.prepare context, mesh, model if layer.prepare
      passes = layer.numPasses context, mesh, model
      numPassesRequested = passes if passes > numPassesRequested

    @clearAssigns() # don't taint assigns from one mesh to the next
    mesh.data.context = context # in case it changed - FIXME make this not necessary
    @shader.bind context
    gl = context.gl
    for pass in [0...numPassesRequested]
      continue unless @preparePass context, mesh, model, pass, numPassesRendered
      numPassesRendered++
      @drawBuffers context, mesh, pass
    if numPassesRendered > 1
      gl.blendFunc GL_SRC_ALPHA, GL_ONE_MINUS_SRC_ALPHA
      gl.depthFunc GL_LESS
      
    numPassesRendered
    
  preparePass: (context, mesh, model, pass, numPassesRendered = 0) ->
    for layer in @layers
      if result = (layer.setup context, mesh, model, pass) is false
        return false
    if numPassesRendered is 1
      gl = context.gl
      gl.blendFunc GL_ONE, GL_ONE
      gl.depthFunc GL_EQUAL
    @shader.set context, @assigns, pass is 0
    return true
    
  drawBuffers: (context, mesh, pass = 0) ->
    unless <%= Rails.env.production? %> or @_alreadyWarned
      # in development mode, check that all variables are assigned, and warn if not.
      # Only do this once (per material).
      @_alreadyWarned = true
      for name, variable of @shader.variables(context)
        if variable.value is undefined
          console.log "Warning: material #{@name} did not set a value for shader variable #{variable.name}"
      
    if (buffer = mesh.getIndexBuffer()) && buffer.length
      buffer.bind context if pass is 0
      context.gl.drawElements mesh.draw_mode, buffer.length, buffer.dataType, 0
    else if length = mesh.data.vertexBuffer?.length
      context.gl.drawArrays mesh.draw_mode, 0, length
    
  ###
  Renders the given mesh and its sub-mesh, if any, and then returns
  the total number of render passes that were required.
  ###
  render: (context, mesh, model) ->
    numPassesRendered = 0
    while mesh
      numPassesRendered += @renderMesh context, mesh, model
      mesh = mesh.submesh
    numPassesRendered
  
  @instances: {}
  @resources: {}
  @__isMaterial: true
  
  @find: (name) ->
    return Jax.Material.instances[name] if Jax.Material.instances[name]
    throw new Error "Material '#{name}' could not be found!" unless data = Jax.Material.resources[name]
    # FIXME Messy...
    switch data.type?.toString().toUpperCase()
      when 'CUSTOM'  then Klass = Jax.Material.Custom
      when 'LEGACY'  then Klass = Jax.Material.Legacy
      when 'SURFACE' then Klass = Jax.Material.Surface
      when 'HALO'    then Klass = Jax.Material.Halo
      when 'WIRE'    then Klass = Jax.Material.Wire
      else
        # last-ditch effort to find custom materials
        unless Klass = Jax.Material[data.type] and Klass.__isMaterial
          console.log "#{name}: Material type #{data.type} is invalid. Please note that its meaning changed in Jax v3.0; it should be one of 'Surface', 'Legacy', 'Wire', 'Halo', 'Custom'."
          console.log "Type of material #{name} is defaulting to 'Legacy'."
          Klass = Jax.Material.Legacy
    throw new Error "#{name}: Material type #{data.type} is not yet implemented." unless Klass
    Jax.Material.instances[name] = new Klass data, name
    
  @addResources: (resources) ->
    for name, data of resources
      # throw new Error "Duplicate material name: #{name}" if Jax.Material.resources[name]
      Jax.Material.resources[name] = data

  @clearResources: ->
    Jax.Material.resources = {}
    Jax.Material.instances = {}
  